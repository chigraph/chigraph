set(CG_USE_SYSTEM_BOOST @CG_USE_SYSTEM_BOOST@)
set(CG_USE_SYSTEM_LIBGIT2 @CG_USE_SYSTEM_LIBGIT2@)
set(CG_LLVM_VERSION @LLVM_VERSION@)
set(CG_LLVM_COMPONENTS @LLVM_COMPONENTS@)
set(CG_BUILD_DEBUGGER @CG_BUILD_DEBUGGER@)


get_filename_component(CG_INSTALL_DIRECTORY "${CMAKE_CURRENT_LIST_FILE}" PATH)
get_filename_component(CG_INSTALL_DIRECTORY "${CG_INSTALL_DIRECTORY}" PATH)
get_filename_component(CG_INSTALL_DIRECTORY "${CG_INSTALL_DIRECTORY}" PATH)
get_filename_component(CG_INSTALL_DIRECTORY "${CG_INSTALL_DIRECTORY}" PATH)

message(STATUS "Chigraph directory: ${CG_INSTALL_DIRECTORY}")

# setup boost
if (CG_USE_SYSTEM_BOOST)
	find_package(Boost COMPONENTS system filesystem)
else()
	add_library(Boost::system STATIC IMPORTED)
	set_property(TARGET Boost::system PROPERTY IMPORTED_LOCATION ${CG_INSTALL_DIRECTORY}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}boost_system${CMAKE_STATIC_LIBRARY_SUFFIX})
	set_property(TARGET Boost::system PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${CG_INSTALL_DIRECTORY}/include)
	
	add_library(Boost::filesystem STATIC IMPORTED)
	set_property(TARGET Boost::filesystem PROPERTY IMPORTED_LOCATION ${CG_INSTALL_DIRECTORY}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}boost_filesystem${CMAKE_STATIC_LIBRARY_SUFFIX})
	set_property(TARGET Boost::filesystem PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${CG_INSTALL_DIRECTORY}/include)
	set_property(TARGET Boost::filesystem PROPERTY INTERFACE_LINK_LIBRARIES Boost::system)
endif()


# setup libgit2
if (CG_USE_SYSTEM_LIBGIT2)
	find_package(PkgConfig)
	pkg_search_module(PC_LIBGIT2 libgit2)

	set(LIBGIT2_DEFINITIONS ${PC_LIBGIT2_CFLAGS_OTHER})

	find_path(LIBGIT2_INCLUDE_DIR NAMES git2.h
		HINTS
		${PC_LIBGIT2_INCLUDEDIR}
		${PC_LIBGIT2_INCLUDE_DIRS}
	)

	find_library(LIBGIT2_LIBRARY NAMES git2
	HINTS
		${PC_LIBGIT2_LIBDIR}
		${PC_LIBGIT2_LIBRARY_DIRS}
	)

else()
	set(LIBGIT2_INCLUDE_DIR ${CG_INSTALL_DIRECTORY}/include)
	find_library(LIBGIT2_LIBRARY NAMES git2 HINTS ${CG_INSTALL_DIRECTORY}/lib NO_DEFAULT_PATH)
endif()

add_library(libgit2 SHARED IMPORTED)
set_property(TARGET libgit2 PROPERTY IMPORTED_LOCATION ${LIBGIT2_LIBRARY})
set_property(TARGET libgit2 PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${LIBGIT2_INCLUDE_DIR})

# setup threads
find_package(Threads REQUIRED)

# setup libexecstream
find_library(LIBEXECSTREAM_LIBRARY NAMES libchigraphexecstream HINTS ${CG_INSTALL_DIRECTORY}/lib NO_DEFAULT_PATH)
add_library(libchigraphexecstream STATIC IMPORTED)
set_property(TARGET libchigraphexecstream PROPERTY IMPORTED_LOCATION ${LIBEXECSTREAM_LIBRARY})
set_property(TARGET libchigraphexecstream PROPERTY INTERFACE_LINK_LIBRARIES Threads::Threads)

# setup LLVM
if (NOT LLVM_CONFIG)
	find_program(LLVM_CONFIG llvm-config REQUIRED)
endif()

execute_process(COMMAND ${LLVM_CONFIG} --version OUTPUT_VARIABLE LLVM_VERSION OUTPUT_STRIP_TRAILING_WHITESPACE)

if (NOT LLVM_VERSION VERSION_EQUAL CG_LLVM_VERSION)
	message(FATAL_ERROR "LLVM version mismatch detected. Chigraph built with ${CG_LLVM_VERSION} and we only found LLVM ${LLVM_VERSION} with llvm-config in ${LLVM_CONFIG}")
endif()

execute_process(COMMAND ${LLVM_CONFIG} --libs ${CG_LLVM_COMPONENTS} OUTPUT_VARIABLE LLVM_LIBRARIES OUTPUT_STRIP_TRAILING_WHITESPACE)
string(REPLACE " " ";" LLVM_LINK_LIST "${LLVM_LIBRARIES}")
message(STATUS "LLVM link libraries: ${LLVM_LINK_LIST}")

# get system libraries to link to
execute_process(COMMAND ${LLVM_CONFIG} --system-libs OUTPUT_VARIABLE LLVM_SYSTEM_LIBS OUTPUT_STRIP_TRAILING_WHITESPACE)
string(REPLACE " " ";" LLVM_SYSTEM_LINK_LIST "${LLVM_SYSTEM_LIBS}")
message(STATUS "LLVM system libraries: ${LLVM_SYSTEM_LINK_LIST}")

# get preprocessor flags
execute_process(COMMAND ${LLVM_CONFIG} --cppflags OUTPUT_VARIABLE LLVM_CXX_FLAGS OUTPUT_STRIP_TRAILING_WHITESPACE)
string(REPLACE " " ";" LLVM_CXX_FLAGS_LIST "${LLVM_CXX_FLAGS}")
message(STATUS "LLVM cxx flags: ${LLVM_CXX_FLAGS_LIST}")

# get ld flags
execute_process(COMMAND ${LLVM_CONFIG} --ldflags OUTPUT_VARIABLE LLVM_LD_FLAGS OUTPUT_STRIP_TRAILING_WHITESPACE)
string(REPLACE " " ";" LLVM_LD_FLAGS_LIST "${LLVM_LD_FLAGS}")
message(STATUS "LLVM ld flags: ${LLVM_LD_FLAGS_LIST}")

# setup chigraph 
add_library(chigraph STATIC IMPORTED)
set_property(TARGET chigraph PROPERTY IMPORTED_LOCATION ${CG_INSTALL_DIRECTORY}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}chigraph${CMAKE_STATIC_LIBRARY_SUFFIX})
set_property(TARGET chigraph PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${CG_INSTALL_DIRECTORY}/include)
set_property(TARGET chigraph PROPERTY INTERFACE_LINK_LIBRARIES libgit2 Boost::filesystem libchigraphexecstream ${LLVM_LD_FLAGS_LIST} ${LLVM_SYSTEM_LINK_LIST} ${LLVM_LINK_LIST})
set_property(TARGET chigraph PROPERTY INTERFACE_COMPILE_OPTIONS ${LLVM_CXX_FLAGS_LIST})

if (CG_BUILD_DEBUGGER)


	execute_process(COMMAND ${LLVM_CONFIG} --libdir OUTPUT_VARIABLE LLVM_LIB_DIR OUTPUT_STRIP_TRAILING_WHITESPACE)

	# find LLDB
	find_library(LLDB_LIBRARY liblldb.so.1 lldb HINTS ${LLVM_LIB_DIR} NO_DEFAULT_PATH)
	message(STATUS "LLDB library: ${LLDB_LIBRARY}")

	# setup chigraphdebugger
	add_library(chigraphdebugger STATIC IMPORTED)

	set_property(TARGET chigraphdebugger PROPERTY IMPORTED_LOCATION ${CG_INSTALL_DIRECTORY}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}chigraphdebugger${CMAKE_STATIC_LIBRARY_SUFFIX})
	set_property(TARGET chigraphdebugger PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${CG_INSTALL_DIRECTORY}/include)
	set_property(TARGET chigraphdebugger PROPERTY INTERFACE_LINK_LIBRARIES ${LLDB_LIBRARY} chigraph)
endif()
